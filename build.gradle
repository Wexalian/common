import java.time.Duration

buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group = 'com.wexalian.common'
version = '0.0.11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.wexalian.nullability:nullability:0.0.2'

    implementation 'com.google.code.gson:gson:2.9.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.9.0'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'common'
            from components.java

            pom {
                name = 'common'
                packaging = 'jar'
                description = 'A common library for code used in multiple of my projects'
                url = 'https://www.github.com/Wexalian/common'
                licenses {
                    license {
                        name = 'The Unlicense'
                        url = 'https://unlicense.org/'
                    }
                }
                developers {
                    developer {
                        id = 'Wexalian'
                        name = 'Lars Gerrits'
                        email = 'larsgerrits99@gmail.com'
                        organization = 'wexalian'
                        organizationUrl = 'https://www.wexalian.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Wexalian/common.git'
                    developerConnection = 'scm:git:ssh://github.com/Wexalian/common.git'
                    url = 'https://www.github.com/Wexalian/common'
                }
            }
        }
    }
}


nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = deployRepoUsername
            password = deployRepoPassword
        }
    }
    transitionCheckOptions {
        maxRetries.set(40)
        delayBetween.set(Duration.ofMillis(3000))
    }
}

signing {
    sign publishing.publications.mavenJava
}

//configurations {
//    testCompile
//}